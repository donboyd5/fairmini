US MODEL JANUARY 31, 2025
SPACE MAXVAR=500 MAXS=30 MAXCOEF=30  MAXFSR=60 FIRSTPER=1952.1 LASTPER=2027.4;
@
@ For solution, do at least 3 iterations, and check only first 30 variables.
@
SETUPSOLVE MINITERS=3 MAXCHECK=30 ;
@
@ No missing values in FAIRMODEL
@
@SETUPEST NOMISS;
SETUPSOLVE NOMISS;
@
@ Setup for 2SLS estimation
@
SETUPEST ALT2SLS
@SETUPEST DIVIDET;
@
@ Load the data
@
SMPL 1952.1 2024.4;
LOADDATA FILE=FMDATA.TXT;
@
@ Load the age variables.  They are forecast through 2027.4;
@
LOADDATA FILE=FMAGE.TXT;
@
@ Set up the model
@
SMPL 1952.1 2024.4;
CREATE CNST2CS=1;
CREATE CNST2L2=1;
CREATE CNST2KK=1;
CREATE TBL2=1;
CREATE C=1;
GENR CSZ=CS/POP   ;
GENR CNZ=CN/POP   ;
GENR CDZ=CD/POP   ;
GENR IHHZ=IHH/POP ;
GENR L1Z=L1/POP1  ;
GENR L2Z=L2/POP2  ;
GENR L3Z=L3/POP3  ;
GENR LL1Z=LOG(L1Z);
GENR LL2Z=LOG(L2Z);
GENR LL3Z=LOG(L3Z);
GENR LMZ=LM/POP   ;
GENR LLMZ=LOG(LMZ);
GENR LPF=LOG(PF)       ;
GENR LJF1=LOG(JF/JF(-1)) ;
GENR LHF1=LOG(HF/HF(-1)) ;
GENR LHFL1A=LOG(HF(-1)/HFS(-1));
GENR LHO=LOG(HO)         ;
GENR LWF=LOG(WF)         ;
GENR LWFQ=LOG(WF)-LOG(LAM);
GENR LMFZ=LOG(MF/PF)     ;
GENR LY1=LOG(Y/Y(-1))    ;
GENR LHF=LOG(HF)         ;
GENR LXMFAZ=LOG((X-FA)/POP)      ;
GENR RB1=RB-RB(-1)       ;
GENR RS1=RS-RS(-1);
GENR LCURZ=LOG(CUR/(POP*PF)) ;
GENR IMZ=IM/POP          ;
GENR LUB=LOG(UB)         ;
GENR GAP=(YS-Y)/YS;
GENR XMFAZ=(X-FA)/POP            ;
GENR LXMFA=LOG(X-FA)           ;
GENR UMM=(ABS(U-1.0)+U-1.0)/2 + 1.0;
GENR LU=LOG(UMM);
GENR LWFD5=LOG(WF*(1+D5G)) - LOG(LAM);
GENR PCM1L1A=D794823*PCM1(-1) ;
GENR LMFL1Q=LOG(MF(-1)/PF)           ;
GENR LCURL1Q=LOG(CUR(-1)/(POP(-1)*PF)) ;
GENR LEXL=LOG(JF/(JHMIN/HFS))   ;
GENR LY=LOG(Y)            ;
GENR LYZ=LOG(Y/POP);
GENR YDZ=YD/(POP*PH)      ;
GENR AA1Z=AA1/POP;
GENR AA2Z=AA2/POP; 
GENR AAZ=AA/POP           ;
GENR KHZ=KH/POP           ;
GENR KDZ=KD/POP;
GENR LPIM=LOG(PIM)        ;
GENR LPIMZ=LOG(PIM/PF);
GENR LYDZ=LOG(YD/(POP*PH)) ;
GENR RSB=RS*(1-D2G-D2S)    ;
GENR EXKK=KK-KKMIN         ;
GENR RBA=RB*(1-D2G-D2S)        ;
GENR PCPD=100*((PD/PD(-1))**4-1) ;
GENR PCPD4=100*(PD/PD(-4)-1);
GENR PCPD8=100*((PD/PD(-8))**.5-1);
GENR RBA4=RBA-PCPD4;
GENR Y1=Y-Y(-1)            ;
GENR COGS=COG+COS         ;
GENR LCOGSZ=LOG(COGS/POP)       ;
GENR JHGSZ=(JG*HG+JM*HM+JS*HS)/POP ;
GENR LJHGSZ=LOG(JHGSZ)              ;
GENR TRGSZ=(TRGH+TRSH)/(POP*PH) ;
GENR LTRGSZ=LOG(TRGSZ)         ;
GENR EXL=JF-JHMIN/HFS             ;
GENR LEX=LOG(EX)               ;
GENR LEXZ=LOG(EX/POP);
GENR TFGS=TFG+TFS              ;
GENR LCSZ=LOG(CSZ)             ;
GENR LAAZ=LOG(AAZ)             ;
GENR LCNZ=LOG(CNZ)             ;
GENR LCDZ=LOG(CDZ)             ;
GENR LKHZ=LOG(KHZ)             ;
GENR LIHHZ=LOG(IHHZ)           ;
GENR LV=LOG(V)                 ;
GENR LVZ=LOG(V/X);
GENR LEXKK=LOG(KK/KKMIN)       ;
GENR LIMZ=LOG(IMZ)             ;
GENR LXXXZ=LOG((CS+CN+CD+IHH+IKF+IKH+IKB+IHF)/POP);
GENR WAZPH=WA/PH;
GENR LWAZPH=LOG(WAZPH);
GENR LKDZ=LOG(KDZ);
GENR LDF=LOG(DF);
GENR LDF1=LOG((DF)/(DF(-1)));
GENR LPFZPIM=LOG(PF/PIM);
GENR LWFZPF=LWF-LPF;
GENR LWFQZPF=LWFQ-LPF;
GENR RBMRSL2=RB-RS(-2);
GENR RSMRSL2=RS-RS(-2);
GENR RSLMRSL2=RS(-1)-RS(-2);
GENR RMMRSL2=RM-RS(-2);
GENR RBLMRSL2=RB(-1)-RS(-2);
GENR RMLMRSL2=RM(-1)-RS(-2);
GENR RMACDZ=RMA*.01*T;
GENR RMALIHHZ=RMA(-1)*.002*T;
GENR LKK=LOG(KK);
GENR LPH=LOG(PH);
GENR UR1=UR-UR(-1);
GENR PIEFA=PIEF-TFG-TFS-TFR  ;
GENR PIEFAMM=(ABS(PIEFA-4.)+PIEFA-4.)/2+4.;
GENR LPIEFAZ=LOG(PIEFAMM/DF(-1));
GENR AAF=-AF;
GENR AAG=-AG;
GENR INTFZ=INTF/AAF;
GENR INTGZ=INTG/AAG;
GENR RQG=(.4*(RS/400)+.75*.6*(1/8)*(1/400)*(RB
 +RB(-1)+RB(-2)+RB(-3)+RB(-4)+RB(-5)+RB(-6)+RB(-7)));
GENR LX=LOG(X);
GENR CGZ3=(CG+CG(-1)+CG(-2))/(PX*YS+PX(-1)*YS(-1)+PX(-2)*YS(-2));
GENR LKK1=LKK-LKK(-1);
GENR ONEZUR=1/UR;
GENR PCM1L1B=D20083*PCM1(-1) ;
GENR PCPF=100*((PF/PF(-1))**4-1) ;
GENR PCY=100*((Y/Y(-1))**4-1) ;
GENR PCPF4=100*((PF/PF(-4))**1-1)    ;
GENR PCY4=100*((Y/Y(-4))**1-1)    ;
GENR ONEZGAP=1/(GAP+.07);
@ Only for testing
@GENR ONEZUR=ONEZGAP;
@
EQ 1 LCSZ  CNST2CS C AG1 AG2 AG3 LCSZ(-1) LYDZ RSA LAAZ(-1) RHO=1;
LHS CS=EXP(LCSZ)*POP;
EQ 2 LCNZ  C AG1 AG2 AG3 LCNZ(-1) LAAZ(-1) LYDZ RMA RHO=1;
LHS CN=EXP(LCNZ)*POP;
EQ 3 LCDZ C AG1 AG2 AG3 LCDZ(-1) LYDZ RMA ;
LHS CD=EXP(LCDZ)*POP;
EQ 4 LIHHZ C AG1 AG2 AG3 LIHHZ(-1) LYDZ RMA(-1) RHO=1;
LHS IHH=EXP(LIHHZ)*POP;
EQ 5 LL1Z  C LL1Z(-1) LAAZ(-1) UR ;
LHS L1=EXP(LL1Z)*POP1;
EQ 6 LL2Z  CNST2L2 C TBL2 T LL2Z(-1) LAAZ(-1) UR ;
LHS L2=EXP(LL2Z)*POP2;
EQ 7 LL3Z  C LL3Z(-1) LWAZPH LAAZ(-1) UR;
LHS L3=EXP(LL3Z)*POP3;
EQ 8 LLMZ  C LLMZ(-1) UR ;
LHS LM=EXP(LLMZ)*POP;
EQ 9 NONE9 C;
EQ 10 LPF LPF(-1) LWFD5 C T LPIM  ONEZUR RHO=1;
LHS PF=EXP(LPF);
EQ 11 LY  C LY(-1) LX LV(-1) D593 D594 D601 RHO=3;
LHS Y=EXP(LY);
EQ 12 LKK1  CNST2KK C LEXKK(-1) LKK1(-1) LY1 LY1(-1) LY1(-2) 
LY1(-3) LY1(-4) CGZ3(-2) RHO=1;
LHS KK=EXP(LKK1)*KK(-1);
EQ 13 LJF1  C LEXL(-1) LJF1(-1) LY1 D593 ;
LHS JF=EXP(LJF1)*JF(-1);
EQ 14 LHF1  C LHFL1A LEXL(-1) LY1 T ;
LHS HF=EXP(LHF1)*HF(-1);
EQ 15 LHO  C HFF HFF(-1) RHO=1;
LHS HO=EXP(LHO);
EQ 17 LMFZ  C LMFZ(-1) LXMFA  RSB ;
LHS MF=EXP(LMFZ)*PF;
EQ 18 LDF1  LPIEFAZ ; 
LHS DF=EXP(LDF1)*(DF(-1));
EQ 19 NONE19 C;
EQ 20 NONE20 C;
EQ 21 NONE21 C;
EQ 22 NONE22 C;
EQ 23 RBMRSL2  C RBLMRSL2 RSMRSL2 RSLMRSL2 RHO=1;
LHS RB=RBMRSL2+RS(-2); 
EQ 24 RMMRSL2  C RMLMRSL2 RSMRSL2 RSLMRSL2;
LHS RM=RMMRSL2+RS(-2);
EQ 25 NONE25 C;
EQ 26 LCURZ  C LCURZ(-1) LXMFAZ RSA ;
LHS CUR=EXP(LCURZ)*(POP*PF);
EQ 27 LIMZ  C AG1 AG2 AG3 LIMZ(-1) LYZ LAAZ(-1) LPFZPIM T 
D691 D692 D714 D721 RHO=0; 
LHS IM=EXP(LIMZ)*POP;
EQ 28 LUB  C LUB(-1) LU LWF RHO=1;
LHS UB=EXP(LUB);
EQ 29 INTGZ  C INTGZ(-1) RQG RHO=1;
LHS INTG=(INTGZ)*AAG*1.0;
EQ 30 RS  C RS(-1) PCPD UR UR1 PCM1L1B PCM1L1A RS1(-1) RS1(-2) ;
LHS RS=(ABS(RS-.0)+RS-.0)/2.+.0;
@
@ Beginning with equation 31
@
IDENT PX=(PF*(X-FA)+PFA*FA)/X;
IDENT PEX=PSI1*PX            ;
IDENT PD=(PX*X-PEX*EX+PIM*IM)/(X-EX+IM);
IDENT PH=(PCS*CS+PCN*CN+PCD*CD+PIH*IHH+IBTG+IBTS)/(CS+CN+CD+IHH);
IDENT PCS=PSI2*(1+D3G+D3S)*PD                                   ;
IDENT PCN=PSI3*(1+D3G+D3S)*PD                                   ;
IDENT PCD=PSI4*(1+D3G+D3S)*PD                                   ;
IDENT PIH=PSI5*PD                                               ;
IDENT PIK=PSI6*PD                                               ;
IDENT PG=PSI7*PD                                                ;
IDENT PS=PSI8*PD                                                ;
IDENT PIV=PSI9*PD                                               ;
IDENT WH=100*((WF*JF*(HN+1.5*HO)+WG*JG*HG+WM*JM*HM+WS*JS*HS)/
(JF*(HN+1.5*HO)+JG*HG+JM*HM+JS*HS))                             ;
IDENT WG=PSI10*WF                                               ;
IDENT WM=PSI11*WF                                               ;
IDENT WS=PSI12*WF                                               ;
IDENT THG=D1G*YT;
IDENT THS=D1S*YT;
IDENT TFG=D2G*(PIEF-TFS)                                        ;
IDENT TFS=D2S*PIEF                                              ;
IDENT IBTG=(D3G/(1+D3G))*(PCS*CS+PCN*CN+PCD*CD-IBTS)            ;
IDENT IBTS=(D3S/(1+D3S))*(PCS*CS+PCN*CN+PCD*CD-IBTG)            ;
IDENT SIHG=D4G*(WF*JF*(HN+1.5*HO))               ;
IDENT SIFG=D5G*(WF*JF*(HN+1.5*HO))                    ;
IDENT PKH=PSI14*PD;
IDENT INTGR=PSI15*INTG;
IDENT USROW=-INTGR+DR+PIEFRET+USOTHER;
IDENT KD=(1-DELD)*KD(-1)+CD                                     ;
IDENT KH=(1-DELH)*KH(-1)+IHH                                    ;
IDENT X=CS+CN+CD+IHH+IKF+EX-IM+COG+COS+IKH+IKB+IKG+IHF;
IDENT XX=PCS*CS+PCN*CN+PCD*CD+PIH*(IHF+IHH)+PIK*(IKH+IKG+IKB+IKF)
 +PEX*EX-PIM*IM+PG*COG+PS*COS-IBTG-IBTS;
IDENT HN=HF-HO                                                  ;
IDENT V=V(-1)+Y-X ;
IDENT YT=WF*JF*(HN+1.5*HO)+WG*JG*HG+WM*JM*HM+WS*JS*HS
+RNT+INTZ+INTF+INTG-INTGR+INTS+DF+DB+DR+DG+DS+TRFH-TRHR-SIGG-SISS;
IDENT SH=YT-SIHG-SIHS-THG-THS
-PCS*CS-PCN*CN-PCD*CD+TRGH+TRSH+UB
+INS+NICD+CCH-CTH-PIH*IHH-CDH-PIK*IKH-NNH;
IDENT AH=AH(-1)+SH-MH+MH(-1)+CG-DISH                            ;
IDENT CCF1=D6G*(PIK*IKF+PIK(-1)*IKF(-1)+PIK(-2)*IKF(-2)+PIK(-3)*IKF(-3))/4.;
IDENT PIEF=XX+PIV*IVF+SUBS+SUBG+USOTHER
-WF*JF*(HN+1.5*HO)-RNT-INTZ-INTF-TRFH-NICD-CCH+CDH
-TBS-TRFS-CCS-TRFR-DB-GSB-CTGB
-GSMA-GSCA-TBG-TRFG-CCG
-SIFG-SIFS
-GSNN-IVA-CCF1-STAT
+TTRRF
;
IDENT SF=XX+SUBS+SUBG+USOTHER+PIEFRET
-WF*JF*(HN+1.5*HO)-RNT-INTZ-INTF-TRFH-NICD-CCH+CDH
-TBS-TRFS-CCS
-TRFR
-DB-GSB-CTGB
-GSMA-GSCA-TBG-TRFG-CCG
-SIFG-SIFS
-STAT
-DF-TF1
-PIK*IKF-PIH*IHF-NNF-CTF1-CTNN
+TTRRF
;
IDENT AF=AF(-1)+SF-MF+MF(-1) -DISF;
IDENT MB=MB(-1) -MH+MH(-1) -MF+MF(-1) -MR+MR(-1) -MG+MG(-1) -MS+MS(-1)
 +CUR-CUR(-1);
IDENT SB=GSB-CTB-PIK*IKB;
IDENT AB=AB(-1)+SB-MB+MB(-1) -(BR-BO)+(BR(-1) -BO(-1)) -DISB ;
IDENT SR=-PEX*EX-USROW+PIM*IM+TFR+TRFR+TRHR+TRGR-TRRG2-CTR-NNR-TRRS-TTRRF;
IDENT AR=AR(-1)+SR-MR+MR(-1)+Q-Q(-1) -DISR                     ;
IDENT SG=GSMA+GSCA+THG+IBTG+TBG+TFG+SIHG+SIFG-DG+TRFG
-PG*COG-WG*JG*HG-WM*JM*HM
-TRGH-UB-TRGR-TRGS-INTG-SUBG
+CCG-INS-CTGMB-NNG-PIK*IKG+SIGG
+CTGB
;
IDENT AG=AG(-1)+SG-MG+MG(-1)+CUR-CUR(-1)+(BR-BO) -(BR(-1) -BO(-1))
 -Q+Q(-1) -DISG ;
IDENT SS=THS+IBTS+TBS+TFS+SIHS+SIFS-DS+TRGS+TRFS
-PS*COS-WS*JS*HS
-TRSH-INTS-SUBS
+CCS-CTS-NNS+SISS+TRRS;
IDENT AS=AS(-1)+SS-MS+MS(-1) -DISS                               ;
IDENT TESTZERO=SH+SF+SB+SR+SG+SS+STAT+TRRG2;
IDENT M1=M1(-1)+MH-MH(-1)+MF-MF(-1)+MR-MR(-1)+MS-MS(-1)+MDIF    ;
IDENT GDP=XX+PIV*(V-V(-1))+IBTG+IBTS+WG*JG*HG+WM*JM*HM+WS*JS*HS;
IDENT GDPR=Y+PSI13*(JG*HG+JM*HM+JS*HS) +STATP      ;
IDENT GDPD=GDP/GDPR                                             ;
IDENT E=JF+JG+JM+JS-LM                                          ;
IDENT U=L1+L2+L3-E                                              ;
IDENT UR=U/(L1+L2+L3-AFT)                                        ;
IDENT AA1=(AH+MH)/PH;
IDENT AA2=(KH*PKH)/PH ;
IDENT IKF=KK-(1-DELK)*KK(-1);
IDENT KKMIN=Y/MUH                                               ;
IDENT JHMIN=Y/LAM                                               ;
IDENT CDH=THETA2*PCD*CD;
IDENT NICD=THETA3*PCD*CD;
IDENT HFF=HF-HFS                                           ;
IDENT TF1=TFG+TFS+TFR;
IDENT TCG=TFG+TBG                                           ;
IDENT SIG=SIHG+SIFG+SIGG                                        ;
IDENT PUG=PG*COG+WG*JG*HG+WM*JM*HM;
IDENT RECG=THG+TCG+IBTG+SIG+TRFG-DG;
IDENT EXPG=PUG+TRGH+TRGR+TRGS+INTG+SUBG-IGZ+UB               ;
IDENT SGP=RECG-EXPG                                             ;
IDENT TCS=TFS+TBS                                               ;
IDENT SIS=SIHS+SIFS+SISS                                        ;
IDENT PUS=PS*COS+WS*JS*HS;
IDENT PFA=THETA1*GDPD;
IDENT RECS=THS+TCS+IBTS+SIS+TRGS+TRFS-DS;
IDENT EXPS=PUS+TRSH+INTS+SUBS-ISZ                    ;
IDENT SSP=RECS-EXPS                                             ;
IDENT YD=WF*JF*(HN+1.5*HO)+WG*JG*HG+WM*JM*HM+WS*JS*HS
+RNT+INTZ+INTF+INTG-INTGR+INTS+DF+DB+DR+DG+DS
 +TRFH+TRGH+TRSH+UB
-SIHG-SIHS
-THG-THS-TRHR-SIGG-SISS;
IDENT SRZ=(YD-PCS*CS-PCN*CN-PCD*CD)/YD                          ;
IDENT IVF=V-V(-1)                                               ;
IDENT PROD=Y/(JF*HF)                                            ;
IDENT WR=WF/PF                                                  ;
IDENT POP=POP1+POP2+POP3                                        ;
IDENT SHRPIE=((1-D2G-D2S)*PIEF)/(WF*JF*(HN+1.5*HO))             ;
IDENT PCGDPR=100*((GDPR/GDPR(-1))**4-1)                          ;
IDENT PCGDPD=100*((GDPD/GDPD(-1))**4-1)                          ;
IDENT PCM1=100*((M1/M1(-1))**4-1)                                ;
IDENT WA=100*((1-D1G-D1S-D4G)*WF*JF*(HN+1.5*HO)+
(1-D1G-D1S)*(WG*JG*HG+WM*JM*HM+WS*JS*HS-SIGG-SISS))/(JF*(HN
       +1.5*HO)+JG*HG+JM*HM+JS*HS);
IDENT RSA=RS*(1-D1G-D1S)                                      ;
IDENT RMA=RM*(1-D1G-D1S)                                      ;
IDENT GNP=GDP+USROW;
IDENT GNPR=GDPR+USROW/GDPD;
IDENT GNPD=GNP/GNPR;
IDENT PIEFRET=THETA4*PIEF;
IDENT AA=AA1+AA2;
@
@ Begin new variables
@
GENR CCG=CCGQ*GDPD;
GENR CCH=CCHQ*GDPD;
GENR CCS=CCSQ*GDPD;
GENR DB=DBQ*GDPD;
GENR DR=DRQ*GDPD;
GENR GSB=GSBQ*GDPD;
GENR GSNN=GSNNQ*GDPD;
GENR IGZ=IGZQ*GDPD;
GENR INTZ=INTZQ*GDPD;
GENR ISZ=ISZQ*GDPD;
GENR MG=MGQ*GDPD;
GENR MH=MHQ*GDPD;
GENR MR=MRQ*GDPD;
GENR MS=MSQ*GDPD;
GENR Q=QQ*GDPD;
GENR RNT=RNTQ*GDPD;
GENR TBG=TBGQ*GDPD;
GENR TRGH=TRGHQ*GDPD;
GENR TRGS=TRGSQ*GDPD;
GENR TRSH=TRSHQ*GDPD;
GENR RECGZGDP=RECG/GDP;
GENR EXPGZGDP=EXPG/GDP;
GENR SGPZGDP=-SGP/GDP;
GENR AGZGDP=-AG/(4*GDP);
GENR INTGZGDP=INTG/GDP;
GENR SRZGDP=SR/GDP;
GENR ASZGDP=-AS/(4*GDP);
GENR PCGDPR4=100*((GDPR/GDPR(-4))**1-1)    ;
GENR PCGDPD4=100*((GDPD/GDPD(-4))**1-1)    ;
GENR LTOTAL=L1+L2+L3;
GENR PCWF=100*((WF/WF(-1))**4-1) ;
@
@ End new variables
@
@ Read first stage regressor information
@
EQ 1 FSR CNST2CS    
C AG1 AG2 AG3 LCSZ(-1) LAAZ(-1)          RSA(-1) 
 CNST2CS(-1) AG1(-1) AG2(-1) AG3(-1) LAAZ(-2) LCSZ(-2) 
 LCOGSZ(-1) LTRGSZ(-1) LEXZ(-1)
 LPOP  LPOP(-1)
;
EQ 2 
FSR  C AG1 AG2 AG3 LCNZ(-1) LAAZ(-1) LYDZ(-1) RMA(-1) 
 LCOGSZ(-1) LTRGSZ(-1) LEXZ(-1)
 AG1(-1) AG2(-1) AG3(-1) LAAZ(-2) LCNZ(-2) 
;
EQ 3 FSR C AG1 AG2 AG3 LAAZ(-1) LYDZ(-1) RMA(-1)
 LCDZ(-1)
 LCOGSZ(-1) LTRGSZ(-1) LEXZ(-1)
 T
;
EQ 4 FSR C RMA(-1) LYDZ(-1) 
 AG1 AG2 AG3
 LIHHZ(-1) 
 LCOGSZ(-1) LTRGSZ(-1) LEXZ(-1)
 T
 AG1(-1) AG2(-1) AG3(-1) LIHHZ(-2) RMA(-2)
;
EQ 5 FSR  C LL1Z(-1) LAAZ(-1) UR(-1)
 LCOGSZ(-1) LTRGSZ(-1) LEXZ(-1)
;
EQ 6 FSR  C CNST2L2 T TBL2 LL2Z(-1) LAAZ(-1) UR(-1) 
 LCOGSZ(-1) LTRGSZ(-1) LEXZ(-1)
;
EQ 7 FSR  C LL3Z(-1) LAAZ(-1) LWAZPH(-1) UR(-1)
 LCOGSZ(-1) LTRGSZ(-1) LEXZ(-1)
;
EQ 8 FSR  C LLMZ(-1) UR(-1) 
 LCOGSZ(-1) LTRGSZ(-1) LEXZ(-1)
;
EQ 10 FSR LPF(-1) LWFD5(-1) C T LPIM(-1) ONEZUR(-1) UR(-1)
 LCOGSZ(-1) LTRGSZ(-1) LEXZ(-1)
 LPF(-2)
;
EQ 11 FSR    
C LY(-1) LV(-1) LY(-2) LY(-3) LY(-4) LV(-2) LV(-3) LV(-4) 
 D593 D594 D601 D601(-1) D601(-2) D601(-3) 
 LCOGSZ(-1) LTRGSZ(-1) LEXZ(-1)
;
EQ 12 FSR  CNST2KK C LKK(-1) LKK(-2) LY(-1) LY(-2) LY(-3) LY(-4) LY(-5) 
 CGZ3(-2) LEXKK(-1)
 LCOGSZ(-1) LTRGSZ(-1) LEXZ(-1)
 CNST2KK(-1) LEXKK(-2) LKK1(-2) LY1(-5) CGZ3(-3)
;
EQ 13 FSR  C LEXL(-1) LJF1(-1) LY1(-1) D593
 LCOGSZ(-1) LTRGSZ(-1) LEXZ(-1)
;
EQ 14 FSR  C LHFL1A LEXL(-1) LY1(-1) T
 LCOGSZ(-1) LTRGSZ(-1) LEXZ(-1)
;
EQ 17 FSR  C LMFZ(-1) LXMFA(-1) RSB(-1) 
 LCOGSZ(-1) LTRGSZ(-1) LEXZ(-1) LMFL1Q(-1)
;
EQ 18 FSR  LPIEFAZ(-1) C
 LCOGSZ(-1) LTRGSZ(-1) LEXZ(-1)
;
EQ 23 FSR    C RB(-1) RB(-2) RS(-1) RS(-2) RS(-3) 
 LCOGSZ(-1) LTRGSZ(-1) LEXZ(-1)
 PCPD(-1) UR(-1)
 T
 LPIMZ(-1) 
;
EQ 24 FSR  C RM(-1) RS(-1) RS(-2) 
 LCOGSZ(-1) LTRGSZ(-1) LEXZ(-1)
 PCPD(-1) UR(-1)
 T
 LPIMZ(-1)
;
EQ 26 FSR  C LCURZ(-1) LXMFAZ(-1) RSA(-1) LCURL1Q(-1)
 LCOGSZ(-1) LTRGSZ(-1) LEXZ(-1)
;
EQ 27 FSR  C LIMZ(-1) LYZ(-1) LAAZ(-1) LPFZPIM(-1) D691 D692 D714 D721 
 AG1 AG2 AG3 
 LCOGSZ(-1) LTRGSZ(-1) LEXZ(-1)
 T
 LPOP LPOP(-1) LPIM(-1)
 LIMZ(-2)
;
EQ 28 FSR    C LUB(-1) LU(-1) LWF(-1) LUB(-2) 
 LCOGSZ(-1) LTRGSZ(-1) LEXZ(-1)
 LPIMZ(-1) PCPD(-1) 
 T
;
EQ 30 FSR  C RS(-1) PCPD(-1) UR(-1) UR1(-1) 
PCM1L1B PCM1L1A RS1(-1) RS1(-2) 
 LCOGSZ(-1) LTRGSZ(-1) LEXZ(-1)
;
@
@ Begin changing 
@
SMPL 1952.1 2024.4;
CREATE D1=0;
CREATE D2=0;
CREATE D3=0;
CREATE T1= 88 ;
CREATE T2=172;
SMPL 1952.1 1973.4;
CREATE D1=1;
SMPL 1974.1 1994.4;
CREATE D2=1;
SMPL 1995.1 2024.4;
CREATE D3=1;
SMPL 1952.1 2024.4;
@CREATE CNST1=D1-T2*D2/(T1-T2)+T*D2/(T1-T2);
CREATE CNST2CS=D3+T1*D2/(T1-T2)-T*D2/(T1-T2);
@
SMPL 1952.1 2024.4;
CREATE D1=0;
CREATE D2=0;
CREATE D3=0;
CREATE T1= 80 ;
CREATE T2=152;
SMPL 1952.1 1971.4;
CREATE D1=1;
SMPL 1972.1 1989.4;
CREATE D2=1;
SMPL 1990.1 2024.4;
CREATE D3=1;
SMPL 1952.1 2024.4;
@CREATE CNST1=D1-T2*D2/(T1-T2)+T*D2/(T1-T2);
CREATE CNST2L2=D3+T1*D2/(T1-T2)-T*D2/(T1-T2);
@CREATE CNST3=CNST1+CNST2; 
@(equals C)
@CREATE TA=T*CNST1;
CREATE TBL2=T*CNST2L2;
@CREATE TC=TA+TB; 
@(equals T)
@
SMPL 1952.1 2024.4;
CREATE D1=0;
CREATE D2=0;
CREATE D3=0;
CREATE T1= 108;
CREATE T2=144;
SMPL 1952.1 1978.4;
CREATE D1=1;
SMPL 1979.1 1987.4;
CREATE D2=1;
SMPL 1988.1 2024.4;
CREATE D3=1;
SMPL 1952.1 2024.4;
@CREATE CNST1=D1-T2*D2/(T1-T2)+T*D2/(T1-T2);
CREATE CNST2KK=D3+T1*D2/(T1-T2)-T*D2/(T1-T2);
@
MODEQ 1 FSR LAAZ(-3) - LAAZ(-1);
MODEQ 2 FSR LAAZ(-3) - LAAZ(-1);
MODEQ 3 FSR LAAZ(-2) - LAAZ(-1);
MODEQ 5 FSR LAAZ(-2) - LAAZ(-1);
MODEQ 6 FSR LAAZ(-2) - LAAZ(-1);
MODEQ 7 FSR LAAZ(-2) - LAAZ(-1);
MODEQ 27 FSR LAAZ(-2) - LAAZ(-1);
@
MODEQ  1 D20201 D20202 D20203 D20204 D20211 D20212 D20213 D20214 
 FSR D20201 D20202 D20203 D20204 D20211 D20212 D20213 D20214 D20214(-1);
MODEQ  2 D20201 D20202 D20203 D20204 D20211 D20212 D20213 D20214 
 FSR D20201 D20202 D20203 D20204 D20211 D20212 D20213 D20214 D20214(-1);
MODEQ  3 D20201 D20202 D20203 D20204 D20211 D20212 D20213 D20214 
 FSR D20201 D20202 D20203 D20204 D20211 D20212 D20213 D20214 ;
MODEQ  4 D20201 D20202 D20203 D20204 D20211 D20212 D20213 D20214 
 FSR D20201 D20202 D20203 D20204 D20211 D20212 D20213 D20214 D20214(-1);
MODEQ  5 D20201 D20202 D20203 D20204 D20211 D20212 D20213 D20214  
 FSR D20201 D20202 D20203 D20204 D20211 D20212 D20213 D20214  ;
MODEQ  6 D20201 D20202 D20203 D20204 D20211 D20212 D20213 D20214 
 FSR D20201 D20202 D20203 D20204 D20211 D20212 D20213 D20214 ;
MODEQ  7 D20201 D20202 D20203 D20204  D20211 D20212 D20213 D20214 
 FSR D20201 D20202 D20203 D20204 D20211 D20212 D20213 D20214  ;
MODEQ  8 D20201 D20202 D20203 D20204 D20211 D20212 D20213 D20214 
 FSR D20201 D20202 D20203 D20204 D20211 D20212 D20213 D20214 ;
MODEQ 10 D20201 D20202 D20203 D20204 D20211 D20212 D20213 D20214 
 FSR D20201 D20202 D20203 D20204 D20211 D20212 D20213 D20214 D20214(-1);
MODEQ 11 D20201 D20202 D20203 D20204  D20211 D20212 D20213 D20214 
 FSR D20201 D20202 D20203 D20204 D20211 D20212 D20213 D20214 
 D20214(-1) D20214(-2) D20214(-3);
MODEQ 12 D20201 D20202 D20203 D20204 D20211 D20212 D20213 D20214 
 FSR D20201 D20202 D20203 D20204 D20211 D20212 D20213 D20214 D20214(-1);
MODEQ 13 D20201 D20202 D20203 D20204 D20211 D20212 D20213 D20214  
 FSR D20201 D20202 D20203 D20204 D20211 D20212 D20213 D20214 ;
MODEQ 14 D20201 D20202 D20203 D20204 D20211 D20212 D20213 D20214  
 FSR D20201 D20202 D20203 D20204 D20211 D20212 D20213 D20214 ;
MODEQ 15 D20201 D20202 D20203 D20204 D20211 D20212 D20213 D20214 ;
MODEQ 17 D20201 D20202 D20203 D20204 D20211 D20212 D20213 D20214  
 FSR D20201 D20202 D20203 D20204 D20211 D20212 D20213 D20214  ;
MODEQ 18 D20201 D20202 D20203 D20204 D20211 D20212 D20213 D20214 
 FSR D20201 D20202 D20203 D20204 D20211 D20212 D20213 D20214 ;
MODEQ 23 D20201 D20202 D20203 D20204 D20211 D20212 D20213 D20214 
 FSR D20201 D20202 D20203 D20204 D20211 D20212 D20213 D20214 D20214(-1);
MODEQ 24 D20201 D20202 D20203 D20204 D20211 D20212 D20213 D20214 
 FSR D20201 D20202 D20203 D20204 D20211 D20212 D20213 D20214 ;
MODEQ 26 D20201 D20202 D20203 D20204 D20211 D20212 D20213 D20214  
 FSR D20201 D20202 D20203 D20204 D20211 D20212 D20213 D20214  ;
MODEQ 27 D20201 D20202 D20203 D20204 D20211 D20212 D20213 D20214  
 FSR D20201 D20202 D20203 D20204 D20211 D20212 D20213 D20214 ;
MODEQ 29 D20201 D20202 D20203 D20204 D20211 D20212 D20213 D20214 ;
@
@ Estimate the 30 equations 
@
SMPL 1954.1 2024.4;
2SLS; EST 10 PLOT=0; END;
SMPL 1952.1 2024.4;
CREATE BETA1=0;
CREATE BETA2=0;
CREATE BETA1=COEF(1,10);
CREATE BETA2=COEF(2,10);
CREATE DELTA1=BETA1/(1-BETA2);
PRINTVAR STATS BETA1 BETA2 DELTA1 LWFQ LPF;
GENR LWFQZ=LWFQ-DELTA1*LPF(-1);
GENR LPFZ=LPF-DELTA1*LPF(-1);
EQ 16 LWFQZ LWFQZPF(-1) LPFZ  C ;
LHS WF=EXP(LWFQZ+DELTA1*LPF(-1))*LAM;
EQ 16 FSR LWFQZPF(-1) LPF(-1) LPF(-2) C T  
 LCOGSZ(-1) LTRGSZ(-1) LEXZ(-1)
 LPIM(-1) ONEZUR(-1) UR(-1)
;
MODEQ 16 D20201 D20202 D20203 D20204 D20211 D20212 D20213 D20214 
 FSR D20201 D20202 D20203 D20204 D20211 D20212 D20213 D20214 ;
@
SMPL 1954.1 2024.4;
2SLS ;
SMPL 1954.1 2024.4;
EST 1-14 PLOT=0;
SMPL 1956.1 2024.4;
EST 15 PLOT=0;
SMPL 1954.1 2024.4;
EST 16-27 PLOT=0;
SMPL 1954.1 2000.4;
EST 28 PLOT=0;
SMPL 1954.1 2024.4;
EST 29 PLOT=0;
SMPL 1954.1 2008.3; 
EST 30 PLOT=0;
END;
@
@ Test the identities and LHS
@
SMPL 2024.4 2024.4;
TEST IDENT;
TEST LHS;
@
@ Solve the model over the forecast period after choosing the exogenous
@ variable values
@
SMPL 2025.1 2027.4;
EXOGENOUS VARIABLE=LUB;
@EXOGENOUS VARIABLE=RS;
EXTRAPOLATE;
@
@ Read the age variables back in because they have been changed by 
@ EXTRAPOLATE.
@
LOADDATA FILE=FMAGE.TXT;
INPUT FILE=FMEXOG.TXT;   
SMPL 2025.1 2027.4;
GENR CG=.123*PX(-1)*YS(-1);
SETUPSOLVE MINITERS=40;
SOLVE DYNAMIC OUTSIDE FILEVAR=KEYBOARD NORESET;
PCY
PCPF
UR
PIEF
GDPR
;
SETYYTOY;
@
@ Print the model and names
@
PRINTMODEL;
PRINTNAMES;
@
@ You are now ready to begin experimenting with the model. 
@
@QUIT;
@
@ Ignore the following
@
SMPL 1952.1 2027.4;
PRINTVAR FILEOUT=ZAYEV.TXT LOADFORMAT;
QUIT;
